buildscript {
    ext {
        queryDslVersion = "5.0.0"
        springCloudVersion = "2023.0.1"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5' apply false
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.app.preorder'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    // 서비스 모듈(+discovery) 에만 Spring Boot 적용
    if (project.name.endsWith('-service') || project.name == 'discovery-service') {
        apply plugin: 'org.springframework.boot'
    }

    dependencies {
        // lombok 공통
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // 테스트 공통
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.2.5"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
